%{
#include <stdio.h>
#include "util.h"
#include "symbol.h"
#include "errormsg.h"
#include "absyn.h"

extern int yylex(); /* function prototype */

A_exp absyn_root;

void yyerror(const char *s) {
 EM_error(EM_tokPos, "%s", s);
}

%}

%define parse.error verbose

%union {
  int pos;
  int ival;
  string sval;
  struct {string sval; int ssiz; } str;
  A_field field;
  A_fieldList fieldList;
  A_dec dec;
  A_decList decList;
  A_fundecList fundecList;
  A_fundec fundec;
  A_ty ty;
  A_namety namety;
  A_nametyList nametyList;
  A_exp exp;
  A_var var;
  A_efield efield;
  A_efieldList efieldList;
  A_expList expList;
  S_symbol symbol;
}

%type <field> tyfield
%type <fieldList> tyfields tyfieldList
%type <symbol> id
%type <dec> dec vardec
%type <decList> decs
%type <fundec> fundec
%type <ty> ty
%type <namety> tydec
%type <exp> exp funcall binexp reccrea ifexp boolexp expseq program
%type <var> lvalue
%type <efield> recfield
%type <efieldList> recfields
%type <expList> seq funargs

%token <sval> ID
%token <str> STRING
%token <ival> INT

%token
  COMMA "," COLON ":" SEMICOLON ";" LPAREN "(" RPAREN ")" LBRACK "[" RBRACK "]"
  LBRACE "{" RBRACE "}" DOT "."
  PLUS "+" MINUS "-" TIMES "*" DIVIDE "/" EQ "=" NEQ "!=" LT "<" LE "<=" GT ">" GE ">="
  AND "&" OR "|" ASSIGN ":="
  ARRAY "array" IF "if" THEN "then" ELSE "else" WHILE "while" FOR "for" TO "to" DO "do" LET "let" IN "in" END "end" OF "of"
  BREAK "break" NIL "nil"
  FUNCTION "function" VAR "var" TYPE "type"

%right LOWPREC
%nonassoc THEN OF DO TO
%nonassoc ASSIGN
%nonassoc ELSE LBRACK
%left OR
%left AND
%nonassoc EQ NEQ LT LE GT GE
%left PLUS MINUS
%left TIMES DIVIDE
%nonassoc UMINUS

%start program

%%
program : exp        {$$=absyn_root = $1;}

/* QOL */
id : ID   {$$= S_Symbol(String($1));}

/* declarations */
decs : dec decs      {$$=A_DecList($1, $2);}
     |               {$$=NULL;}

dec : tydec          {$$=A_TypeDec(EM_tokPos, A_NametyList($1, NULL));}
    | vardec         {$$=$1;}
    | fundec         {$$=A_FunctionDec(EM_tokPos, A_FundecList($1, NULL));}

/* data types */
tydec : TYPE id EQ ty                    {$$=A_Namety($2, $4);}

ty : id                                  {$$=A_NameTy(EM_tokPos, $1);}
   | LBRACE tyfields RBRACE              {$$=A_RecordTy(EM_tokPos, $2);}
   | ARRAY OF id                         {$$=A_ArrayTy(EM_tokPos, $3);}

tyfields : tyfield tyfieldList           {$$=A_FieldList($1, $2);}
         |                               {$$=NULL;}
tyfield : id COLON id                    {$$=A_Field(EM_tokPos, $1, $3);}
tyfieldList : COMMA tyfield tyfieldList  {$$=A_FieldList($2, $3);}
            |                            {$$=NULL;}

/* variables */
vardec : VAR id ASSIGN exp               {$$=A_VarDec(EM_tokPos, $2, NULL, $4);}
                                         // FIXME
       | VAR id COLON id EQ exp      {$$=A_VarDec(EM_tokPos, $2, $4, $6);}


/* functions */
fundec : FUNCTION id LPAREN tyfields RPAREN EQ exp
           {$$=A_Fundec(EM_tokPos, $2, $4, NULL, $7);}
       | FUNCTION id LPAREN tyfields RPAREN COLON id EQ exp
           {$$=A_Fundec(EM_tokPos, $2, $4, $7, $9);}

/* variables and expression */
lvalue : id         %prec LOWPREC       {$$=A_SimpleVar(EM_tokPos, $1);}
       | lvalue DOT id                  {$$=A_FieldVar(EM_tokPos, $1, $3);}
       | lvalue LBRACK exp RBRACK       {$$=A_SubscriptVar(EM_tokPos, $1, $3);}
       | id LBRACK exp RBRACK           {$$=A_SubscriptVar(EM_tokPos, A_SimpleVar(EM_tokPos, $1), $3);}

/* experessions */

exp : lvalue                            {$$=A_VarExp(EM_tokPos, $1);}
    | NIL                               {$$=A_NilExp(EM_tokPos);}
    | INT                               {$$=A_IntExp(EM_tokPos, $1);}
    | STRING                            {$$=A_StringExp(EM_tokPos, String_strip($1.sval, $1.ssiz));}
    | LPAREN RPAREN                     {$$=NULL;}
    | funcall                           {$$=$1;}
    | binexp                            {$$=$1;}
    | boolexp                           {$$=$1;}
    | reccrea                           {$$=$1;}
    | LPAREN exp seq RPAREN             {$$=A_SeqExp(EM_tokPos, A_ExpList($2, $3));}
    | lvalue ASSIGN exp                 {$$=A_AssignExp(EM_tokPos, $1, $3);}
    | ifexp                             {$$=$1;}
    | WHILE exp DO exp                  {$$=A_WhileExp(EM_tokPos, $2, $4);}
    | FOR id ASSIGN exp TO exp DO exp   {$$=A_ForExp(EM_tokPos, $2, $4, $6, $8);}
    | BREAK                             {$$=A_BreakExp(EM_tokPos);}
    | LET decs IN expseq END            {$$=A_LetExp(EM_tokPos, $2, $4);}
    | id LBRACK exp RBRACK OF exp       {$$=A_ArrayExp(EM_tokPos, $1, $3, $6);}

expseq : exp seq                        {$$=A_SeqExp(EM_tokPos, A_ExpList($1, $2));}
       |                                {$$=NULL;}

seq : SEMICOLON exp seq                 {$$=A_ExpList($2, $3);}
    |                                   {$$=NULL;}

funcall : id LPAREN exp funargs RPAREN  {$$=A_CallExp(EM_tokPos, $1, A_ExpList($3, $4));}
        | id LPAREN RPAREN              {$$=A_CallExp(EM_tokPos, $1, NULL);}

funargs : COMMA exp funargs             {$$=A_ExpList($2, $3);}
        |                               {$$=NULL;}

binexp : MINUS exp %prec UMINUS         {$$=A_OpExp(EM_tokPos, A_minusOp,A_IntExp(EM_tokPos, 0), $2);}
       | exp PLUS exp                   {$$=A_OpExp(EM_tokPos, A_plusOp,$1, $3);}
       | exp MINUS exp                  {$$=A_OpExp(EM_tokPos, A_minusOp,$1, $3);}
       | exp TIMES exp                  {$$=A_OpExp(EM_tokPos, A_timesOp,$1, $3);}
       | exp DIVIDE exp                 {$$=A_OpExp(EM_tokPos, A_divideOp,$1, $3);}
       | exp EQ exp                     {$$=A_OpExp(EM_tokPos, A_eqOp,$1, $3);}
       | exp NEQ exp                    {$$=A_OpExp(EM_tokPos, A_neqOp,$1, $3);}
       | exp GT exp                     {$$=A_OpExp(EM_tokPos, A_gtOp,$1, $3);}
       | exp LT exp                     {$$=A_OpExp(EM_tokPos, A_ltOp,$1, $3);}
       | exp LE exp                     {$$=A_OpExp(EM_tokPos, A_leOp,$1, $3);}
       | exp GE exp                     {$$=A_OpExp(EM_tokPos, A_geOp,$1, $3);}

boolexp : exp AND exp                    {$$=A_IfExp(EM_tokPos, $1, $3, A_IntExp(EM_tokPos, 0));}
        | exp OR exp                     {$$=A_IfExp(EM_tokPos, $1, A_IntExp(EM_tokPos, 1), $3);}


reccrea : id LBRACE recfield recfields RBRACE  {$$=A_RecordExp(EM_tokPos, $1, A_EfieldList($3, $4));}
        | id LBRACE RBRACE                     {$$=A_RecordExp(EM_tokPos, $1, NULL);}

recfield : id EQ exp                           {$$=A_Efield($1, $3);}

recfields : COMMA recfield recfields           {$$=A_EfieldList($2, $3);}
          |                                    {$$=NULL;}

ifexp : IF exp THEN exp                        {$$=A_IfExp(EM_tokPos, $2, $4, NULL);}
      | IF exp THEN exp ELSE exp               {$$=A_IfExp(EM_tokPos, $2, $4, $6);}
